buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3" // shading
}
apply plugin: "net.minecraftforge.gradle.forge"  // adds the forge dependency


version = "1.9.4-0.1.1"
group= "com.derimagia.forgeslack" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "forgeslack"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile 'net.gpedro.integrations.slack:slack-webhook:1.1.1'
    compile 'org.eclipse.jetty:jetty-server:9.2.14.v20151106'
}

minecraft {
    mappings = "snapshot_20160522"

    version = "1.9.4-12.17.0.1940"
    runDir = "run"
}

shadowJar {
    classifier = ""
    relocate "net.gpedro.integrations.slack", "com.derimagia.forgeslack.net.gpedro.integrations.slack"
    relocate "org.eclipse.jetty", "com.derimagia.forgeslack.org.eclipse.jetty"
}

reobf { shadowJar { mappingType = "SEARGE" } }
tasks.reobfShadowJar.mustRunAfter shadowJar
